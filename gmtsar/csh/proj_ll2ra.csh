#!/bin/csh -f 
#       $Id$
#  D. Sandwell 1/12/07
#  M. Wei 1/31/08
#
alias rm 'rm -f'
unset noclobber
if ( -f ~/.quiet ) then
    set V = ""
else
	set V = "-V"
endif

#
#
#  project a grd file from lon/lat coordinates into range/azimuth coordinates
#  this version only works with GMT V4.0 and higher
#
#  Input:
#  trans.dat    - file generated by llt_grid2rat  (r a topo lon lat)
#  phase_ll.grd - a GRD file of phase or anything in longitude/latitude coordinates 
#
#  Output:
#  phase_ra.grd - a GRD file of phase in radar coordinates
#
# check for number of arguments
#
 if ($#argv < 3) then
  echo " "
  echo "Usage: proj_ll2ra.csh trans.dat phase_ll.grd phase_ra.grd" 
  echo " "
  echo "        trans.dat    - file generated by llt_grid2rat  (r a topo lon lat)"
  echo "        phase_ll.grd - a GRD file of phase or anything in lon/lat-coordinates"
  echo "        phase_ra.grd - output a GRD file in radar coordinates" 
  echo " "
  exit 1
 endif 
#
#  extract the phase in the r a positions
#
gmt set FORMAT_GEO_OUT D
gmt grd2xyz $2 -s -bo3f -fg > llp
#
#   make grids of longitude and latitude versus range and azimuth
#
gmt gmtconvert $1 -o3,4,0,1 -bi5d -bo4f > llra
#
set Rra = `gmt gmtinfo llp -I5m -bi3f`
gmt surface llra $Rra -bi3f -I3s -T.50 -Gllr.grd $V -i0-2
gmt surface llra $Rra -bi3f -I3s -T.50 -Glla.grd $V -i0,1,3
#
# Sample the grids, the get the range, azimuth, phase columns and grid

gmt grdtrack llp -nl -Gllr.grd -Glla.grd -bi3f -bo5f -o3,4,2 > rap
#
#
gmt xyz2grd rap `gmt gmtinfo rap -I32/64 -bi3f` -I32/64 -r -G$3 -bi3f
#
# clean
#
rm ll* rap
